------------------------------------------------------------------------------------------------------------------------
-- Copyright (c) 2015, SimMeters.
-- All rights reserved. Released under the BSD license.
-- adi_0000.jal 1.0 01/01/2015 (ADI Indicator with standard RC servo)

-- Redistribution and use in source and binary forms, with or without modification, are permitted provided that the
-- following conditions are met:

-- 1. Redistributions of source code must retain the above copyright notice, this list of conditions and the
-- following disclaimer.

-- 2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the
-- following disclaimer in the documentation and/or other materials provided with the distribution.

-- THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES,
-- INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
-- DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
-- SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
-- SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
-- WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
-- USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
------------------------------------------------------------------------------------------------------------------------
include canaerospace 	-- Constants for CANAerospace
include lru0002       	-- Include For LRU Device Type
include canbus       	-- CanBus Library
include objects      	-- Objects Library

------------------------------------------------------------------------------------------------------------------------
-- Initialization
------------------------------------------------------------------------------------------------------------------------
_usec_delay(1000000)

setup_can_init()
add_can_rxfilter(id_request_bootloader)
add_can_rxfilter(id_body_pitch_angle)
add_can_rxfilter(id_body_roll_angle)
setup_can_end()

set_pin(P21, low)
set_pin(P22, high)

------------------------------------------------------------------------------------------------------------------------
-- Variables
------------------------------------------------------------------------------------------------------------------------
var can_frame cf
var dword cnt   = 0
var dword dst1  = 0
var dword dst2  = 0
var float pit   = 0
var float bnk   = 0

------------------------------------------------------------------------------------------------------------------------
-- Process servos
------------------------------------------------------------------------------------------------------------------------
isr_servo_enable() -- because we use servos
procedure isr_servo_process() is pragma interrupt

    if (INTCON_TMR0IF) then

        isr_servo_reset()
        cnt = cnt + 1

        if(cnt >= 2000) then
            cnt = 0
        else

            -- pitch angle
            if (cnt < dst1) then
                LATA_LATA0 = high
            elsif (cnt == dst1) then
                LATA_LATA0 = low
            end if

            -- bank angle
            if (cnt < dst2) then
                LATA_LATA1 = high
            elsif (cnt == dst2) then
                LATA_LATA1 = low
            end if

        end if

    end if

end procedure

------------------------------------------------------------------------------------------------------------------------
-- Main loop
------------------------------------------------------------------------------------------------------------------------
forever loop

    -- request bootloader for auto update (do not remove)
    request_bootloader("adi_0000")

    -- pitch angle
    cf = get_can_frame(id_body_pitch_angle)
    if(cf.dlc > 0) then

        pit = get_can_float(cf) + float(25) -- CANAerospace pitch data
        if(pit >= float(0) & pit <= float(50)) then
            pit = (pit - float(25))
            dst1 = dword((pit * float(-0.5)) + float(127))
        end if

    end if

    -- bank angle
    cf = get_can_frame(id_body_roll_angle)
    if(cf.dlc > 0) then

        bnk = get_can_float(cf) + float(45) -- CANAerospace bank data
        if(bnk >= float(0) & bnk <= float(90)) then
            bnk = (bnk - float(45))
            dst2 = dword((bnk * float(-1.0)) + float(127))
        end if

    end if

end loop